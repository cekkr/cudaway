if(NOT WIN32)
    return()
endif()

set(_cudaway_env_root "")
if(DEFINED CUDAWAY_ROCM_WINDOWS_ROOT)
    set(_cudaway_env_root "${CUDAWAY_ROCM_WINDOWS_ROOT}")
elseif(DEFINED ENV{CUDAWAY_ROCM_WINDOWS_ROOT})
    set(_cudaway_env_root "$ENV{CUDAWAY_ROCM_WINDOWS_ROOT}")
elseif(DEFINED ENV{ROCM_PATH})
    set(_cudaway_env_root "$ENV{ROCM_PATH}")
elseif(DEFINED ENV{HIP_PATH})
    set(_cudaway_env_root "$ENV{HIP_PATH}")
endif()

if(_cudaway_env_root STREQUAL "")
    set(_cudaway_env_root "C:/Program Files/AMD/ROCm")
endif()

file(TO_CMAKE_PATH "${_cudaway_env_root}" _cudaway_hip_root)
set(CUDAWAY_ROCM_WINDOWS_ROOT "${_cudaway_hip_root}" CACHE PATH "Root directory of the AMD HIP SDK on Windows" FORCE)
set(CUDAWAY_HIP_BIN_DIR "${_cudaway_hip_root}/bin" CACHE PATH "HIP SDK bin directory" FORCE)
set(CUDAWAY_HIP_INCLUDE_DIR "${_cudaway_hip_root}/include" CACHE PATH "HIP SDK include directory" FORCE)
set(CUDAWAY_HIP_LIB_DIR "${_cudaway_hip_root}/lib" CACHE PATH "HIP SDK library directory" FORCE)

if(NOT TARGET cudaway_hip_windows)
    add_library(cudaway_hip_windows INTERFACE)
    target_include_directories(cudaway_hip_windows INTERFACE "${CUDAWAY_HIP_INCLUDE_DIR}")
    target_link_directories(cudaway_hip_windows INTERFACE "${CUDAWAY_HIP_LIB_DIR}")
    target_link_libraries(cudaway_hip_windows INTERFACE amdhip64)
    target_compile_definitions(cudaway_hip_windows INTERFACE CUDAWAY_USE_HIP_STUBS=1)
endif()
